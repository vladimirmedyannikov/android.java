buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.0'
        classpath 'io.fabric.tools:gradle:1.25.1'
    }
}

repositories {
    mavenCentral()
    jcenter()
    flatDir {
        dirs 'libs'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
//apply plugin: 'android-apt'
apply plugin: 'jacoco-android' //codeCov


repositories {
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    testBuildType "codecov"

    defaultConfig {
        applicationId "ru.mos.polls"
        versionCode Integer.parseInt(System.getenv("BUILD_NUMBER") ?: "1")
        versionName "2.5.2"
        minSdkVersion 15
        targetSdkVersion 27
        multiDexEnabled true
        resConfigs "en"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    dataBinding {
        enabled = true
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    signingConfigs {
        codecov {
            storeFile file("../debug.keystore")
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    buildTypes {

        customer {
            applicationIdSuffix = ".customer"
            minifyEnabled false
            debuggable true
        }

        debug {
            minifyEnabled false
            shrinkResources false
            debuggable true
        }

        codecov {
            signingConfig signingConfigs.codecov
            minifyEnabled false
            shrinkResources false
            debuggable true
            testCoverageEnabled true
        }
    }

    dexOptions {
//        incremental = true;
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }
    if (project.hasProperty("debugSigningPropertiesPath") && project.hasProperty("releaseSigningPropertiesPath")) {

        File debugPropsFile = new File(System.getenv('HOME') + "/" + project.property("debugSigningPropertiesPath"))
        File releasePropsFile = new File(System.getenv('HOME') + "/" + project.property("releaseSigningPropertiesPath"))

        if (debugPropsFile.exists() && releasePropsFile.exists()) {
            Properties debugProps = new Properties()
            debugProps.load(new FileInputStream(debugPropsFile))

            Properties releaseProps = new Properties()
            releaseProps.load(new FileInputStream(releasePropsFile))

            signingConfigs {
                debug {
                    storeFile file(debugPropsFile.getParent() + "/" + debugProps['keystore'])
                    storePassword debugProps['keystore.password']
                    keyAlias debugProps['keyAlias']
                    keyPassword debugProps['keyPassword']
                }
                release {
                    storeFile file(releasePropsFile.getParent() + "/" + releaseProps['keystore'])
                    storePassword releaseProps['keystore.password']
                    keyAlias releaseProps['keyAlias']
                    keyPassword releaseProps['keyPassword']
                }
            }

            buildTypes {
                release {
                    minifyEnabled false
                    signingConfig signingConfigs.release
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
                }

                customer {
                    applicationIdSuffix = ".customer"
                    signingConfig signingConfigs.debug
                    minifyEnabled false
                    debuggable true
                }

                debug {
                    signingConfig signingConfigs.debug
                }
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    compileTests()
    compileAndroidSdk()
    compileGoogleServices()
    compileRXAndRetrofit()
    compileMosSocialLib()
    compileUtils()
    implementation 'me.ilich:juggler:0.1.22'
}

def compileAndroidSdk() {
    dependencies {
        implementation 'com.android.support.constraint:constraint-layout:1.0.2'
        annotationProcessor 'com.android.databinding:compiler:3.0.1'
        implementation 'com.android.support:multidex:1.0.3'
        implementation 'com.android.support:support-v4:27.1.0'
        implementation 'com.android.support:appcompat-v7:27.1.0'
        implementation 'com.android.support:design:27.1.0'
        implementation 'com.android.support:support-annotations:27.1.0'
        implementation 'com.android.support:cardview-v7:27.1.0'
        implementation 'com.android.support:recyclerview-v7:27.1.0'
        implementation 'com.android.support:gridlayout-v7:27.1.0'
    }
}

def compileGoogleServices() {
    dependencies {
        implementation 'com.google.android.gms:play-services-gcm:11.8.0'
        implementation 'com.google.android.gms:play-services-location:11.8.0'
        implementation 'com.google.android.gms:play-services-analytics:11.8.0'
    }
}

def compileRXAndRetrofit() {
    dependencies {
        implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
        implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
        implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
        implementation 'com.squareup.retrofit2:retrofit:2.3.0'
        implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
        implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
        implementation 'com.squareup.retrofit2:converter-moshi:2.3.0'
        implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
        implementation 'com.google.code.gson:gson:2.8.1'
    }
}

def compileTests() {
    dependencies {
        androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
            exclude group: 'com.android.support', module: 'support-annotations'
        })
        testImplementation 'junit:junit:4.12'
    }
}

def compileUtils() {
    dependencies {
        annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
        implementation 'com.jakewharton:butterknife:8.8.1'
        implementation 'com.googlecode.libphonenumber:libphonenumber:7.4.3'
        implementation 'com.jpardogo.materialtabstrip:library:1.1.0'
        implementation 'de.hdodenhof:circleimageview:2.2.0'
        implementation 'com.theartofdev.edmodo:android-image-cropper:2.6.0'
        implementation 'pub.devrel:easypermissions:1.2.0'
        implementation 'com.firebase:firebase-jobdispatcher-with-gcm-dep:0.7.0'
        implementation 'com.github.bumptech.glide:glide:4.0.0-RC1'
        annotationProcessor 'com.github.bumptech.glide:compiler:4.0.0-RC1'
    }
}

def compileMosSocialLib() {
    dependencies {
        /**
         * Библиотека mos_social для работы с соцсетями
         */
        implementation(name: 'mos_social_0.1.92', ext: 'aar') {
            exclude module: 'support-v4'
        }
        implementation 'com.twitter.sdk.android:twitter:3.0.0'
        implementation 'com.twitter.sdk.android:tweet-ui:3.0.0'
        implementation 'com.twitter.sdk.android:twitter-core:3.0.0'
        implementation 'com.twitter.sdk.android:tweet-composer:3.0.0'
        implementation 'com.vk:androidsdk:1.6.7'
        implementation 'com.facebook.android:facebook-android-sdk:4.31.0'
        implementation 'ru.ok:odnoklassniki-android-sdk:2.0.8'
        implementation 'com.google.android.gms:play-services-auth:11.8.0'
        implementation 'com.squareup.okhttp3:okhttp:3.9.1'
        implementation 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    }
}
